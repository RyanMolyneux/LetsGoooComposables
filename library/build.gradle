plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'maven-publish'
}

def letsGoooComposablesVersion = "3.0.2"

android {
    compileSdk 34
    namespace "com.ryanmolyneux.letsgooocomposables"

    buildFeatures { compose true }

    defaultConfig {
        minSdk 26
        targetSdk 33
        versionCode 1
        versionName letsGoooComposablesVersion

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11.toString()
    }
    composeOptions {
        kotlinCompilerExtensionVersion '1.5.10'
    }
}

dependencies {
    implementation 'androidx.compose.ui:ui:1.6.3'
    implementation 'androidx.compose.ui:ui-text:1.6.3'
    implementation 'androidx.compose.ui:ui-tooling:1.6.3'
    implementation 'androidx.compose.foundation:foundation:1.6.3'
    implementation 'androidx.compose.material:material:1.6.3'
    implementation 'androidx.compose.material:material-icons-core:1.6.3'
    implementation 'androidx.compose.material:material-icons-extended:1.6.3'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

publishing {
    repositories {
        maven {
            name = "MajorProductions"
            url = uri("https://maven.pkg.github.com/RyanMolyneux/MajorProductions")
            credentials {
                username = System.getenv("GIT_USERNAME")
                password = System.getenv("GIT_PUBLISH_PACKAGE_TOKEN")
            }
        }
    }
    publications {
        release(MavenPublication) {
            groupId = "com.ryanmolyneux"
            artifactId = "letsgooocomposables"
            version letsGoooComposablesVersion
            artifact("$buildDir/outputs/aar/library-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.api.allDependencies.each {
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', it.group)
                    dependencyNode.appendNode('artifactId', it.name)
                    dependencyNode.appendNode('version', it.version)
                }
            }
        }
    }
}
